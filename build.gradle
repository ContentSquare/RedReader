apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'pmd'

buildscript {
	repositories {
		maven { url 'https://maven.fabric.io/public' }
	}
	dependencies {
		classpath "io.fabric.tools:gradle:${fabricPluginVersion}"
	}
}

repositories {
	maven { url 'https://maven.fabric.io/public' }
}

android {
	compileSdkVersion rootProject.compileSdkVersion

	def keystorePropertiesFile = rootProject.file("$rootDir/config/keystore.properties")
	def keystoreProperties = new Properties()
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

	signingConfigs {
		release {
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
			storeFile file("$rootDir/${keystoreProperties["storeFile"]}")
		}
	}
	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion rootProject.targetSdkVersion
		vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
	}
	// Flag to tell aapt to keep the attribute ids around
	aaptOptions {
		additionalParameters "--no-version-vectors"
	}

	buildTypes {
		release {
			minifyEnabled true
		}
	}

	compileOptions {
		compileOptions.encoding = "UTF-8"

		// Use Java 1.7, requires minSdk 8
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError false

		// Translations are crowd-sourced
		disable 'MissingTranslation'
	}

	packagingOptions {
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
	}

	sourceSets {
		main {
			assets.srcDirs = ['assets']
		}
	}
}

dependencies {

	implementation project(':library')
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
	implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
	implementation "org.apache.commons:commons-lang3:${apacheCommonsVersion}"
	implementation "joda-time:joda-time:${jodaTimeVersion}"
	implementation "androidx.appcompat:appcompat:${androidXAppCompatVersion}"
	implementation "com.google.android.material:material:${androidXMaterialDesignVersion}"
	implementation "androidx.recyclerview:recyclerview:${androidXRecyclerViewVersion}"

	implementation "com.squareup.okhttp:okhttp:${okHttpVersion}"
	implementation "info.guardianproject.netcipher:netcipher:${netCipherVersion}"

	testImplementation "junit:junit:${jUnitVersion}"
	implementation("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}@aar") {
		transitive = true
	}
}

pmd {
	toolVersion = pmdVersion
	consoleOutput = true
}

task pmd(type: Pmd) {
	dependsOn 'assembleDebug'
	ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
	ruleSets = [] // otherwise defaults clash with the list in rules.xml
	source 'src/main/java/org/quantumbadger'
	include '**/*.java'
}
